#!/usr/bin/env zsh

name=$(basename $0)

# ------------------------------
# Based on @JopStrow/bspswallow
# ------------------------------

source $(which _log)
source $(which _onexit)
source $(which _singleton)
source $(which _string)
source "$(which _xdg)" >/dev/null 2>&1 || true

# ------------------------------

# Setup XDG directories using extension library
xdg-setup-dirs "${name}"

# Backward-compatible aliases
bsp_ventilate_cache_dir="${XDG_CACHE_DIR}"
bsp_ventilate_config_dir="${XDG_CONFIG_DIR}"
bsp_ventilate_data_dir="${XDG_DATA_DIR}"
bsp_ventilate_state_dir="${XDG_STATE_DIR}"

# ------------------------------

bsp_ventilate_inhaled="${bsp_ventilate_state_dir}/inhaled"
bsp_ventilate_no_inhale="${bsp_ventilate_config_dir}/no_inhale"
bsp_ventilate_terminals="${bsp_ventilate_config_dir}/terminals"

# ------------------------------

source "$(which _args)"

# ------------------------------

_singleton_terminate && [[ $? -eq 1 ]] && exit 1

# ------------------------------

[[ ! -d "${bsp_ventilate_config_dir}" ]] && mkdir -p "${bsp_ventilate_config_dir}"
[[ ! -d "${bsp_ventilate_state_dir}" ]] && mkdir -p "${bsp_ventilate_state_dir}"

[[ ! -f "${bsp_ventilate_inhaled}" ]] && touch "${bsp_ventilate_inhaled}"
[[ ! -f "${bsp_ventilate_no_inhale}" ]] && touch "${bsp_ventilate_no_inhale}"
[[ ! -f "${bsp_ventilate_terminals}" ]] && touch "${bsp_ventilate_terminals}"

# ------------------------------

_bsp_ventilate() {

	log-info "subscribing to events"

	bspc subscribe node_add node_remove | while read -r _event; do
		case $(echo "${_event}" | awk '{ print $1 }') in
			node_add)
				_bsp_ventilate_inhale $(echo "${_event}" | awk '{print $5 " " $3}') ;;
			node_remove)
				_bsp_ventilate_exhale $(echo "${_event}" | awk '{print $4 " " $3}') ;;
		esac
	done

}

_bsp_ventilate_class() {
	
	_window=$1
	
	_class='/WM_CLASS/s/\( "\)/\n/g; s/[^\n]*\n\([^"]*\)[^\n]*/\1\n/g'
	_command='/WM_COMMAND/s/[^"]*/\n\n/1; s/\n"//1; s/\n\([^"]*\)".*/\n\1/; /\n/p'
	
	[[ -z "${_window}" ]] && echo "" || \
		xprop -id "${_window}" | \
		sed -n -e "${_class}" -e "${_command}" | \
		sed '/^$/d' | \
		head -n1

}

_bsp_ventilate_inhale() {
	
	_inhaler=$1
	_desktop=$2

	_inhaled=$(bspc query -N -n last)
	
	if [ "${_desktop}" = $(bspc query -D -n last) ]; then

		_inhaler_class=$(_bsp_ventilate_class "${_inhaler}" | string-lowercase)
		_inhaled_class=$(_bsp_ventilate_class "${_inhaled}" | string-lowercase)
		
		log-info "attempting to inhale '%s' by '%s'" "${_inhaled_class}" "${_inhaler_class}"

		grep -Eiq "^${_inhaler_class}$" "${bsp_ventilate_no_inhale}" "${bsp_ventilate_terminals}" && {
			log-info "unable to inhale as '%s' is considered a protected class" "${_inhaler_class}"
			return
		}

		grep -Eiq "^${_inhaled_class}$" "${bsp_ventilate_terminals}" || {
			log-info "unable to inhale as %s is not considered a terminal" "${_inhaled_class}"
			return
		}

		echo "${_inhaler} ${_inhaled} ${_inhaler_class} ${_inhaled_class}" >> ${bsp_ventilate_inhaled}		
		bspc node "${_inhaled}" --flag hidden=on

		log-info "successfully inhaled '%s' with '%s'"  "${_inhaled_class}" "${_inhaler_class}"
	
	fi
}

_bsp_ventilate_exhale() {
	
	_exhaler=$1
	_desktop=$2

	grep -Eiq "^${_exhaler}" "${bsp_ventilate_inhaled}" || {
		log-info "nothing to exhale"
		return
	}

	_exhaler_class=$(grep -Ei "^${_exhaler}" ${bsp_ventilate_inhaled} | head -n1 | awk '{ print $3 }')

	_exhaled=$(grep -Ei "^${_exhaler}" ${bsp_ventilate_inhaled} | head -n1 | awk '{ print $2 }')
	_exhaled_class=$(_bsp_ventilate_class "${_exhaled}" | string-lowercase)

	log-info "attempting to exhale '%s' from '%s'" "${_exhaled_class}" "${_exhaler_class}"

	bspc node "${_exhaled}" --flag hidden=off
	[[ "$(bspc query -D -n "${_exhaled}")" = "${_desktop}" ]] || bspc node "${_exhaled}" -d "${_desktop}"

	bspc node "${_exhaled}" -f
	sed -i "/^${_exhaler}/d" ${bsp_ventilate_inhaled}

	log-info "successfully exhaled '%s' with '%s'" "${_exhaled_class}" "${_exhaler_class}"

}

# ------------------------------

_bsp_ventilate "$@"